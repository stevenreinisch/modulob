grammar de.dubmas.modulob.DataDsl with de.dubmas.modulob.common.Dsl

import 'platform:/resource/de.dubmas.modulob.metamodels/model/Modulob.ecore' as modulob
import 'platform:/resource/de.dubmas.modulob.metamodels/model/ModulobSystem.ecore' as system
import 'platform:/resource/de.dubmas.modulob.metamodels/model/ModulobTypes.ecore' as types

EntityModel returns system::EntityModel:
	(^current ?= 'current')? 'entities for module' 
	module = [system::Module | QualifiedName]
	'version' version = STRING
	(entities += Entity)*
	(enums += Enum)*
;

Entity returns modulob::Entity:
	(comment = DOC_COMMENT)?
	(annotations+=Annotation)*
	(isAbstract?='abstract')? 'entity' name=ID (':' super=[modulob::Entity | QualifiedName])?'{'
		(features+=Feature)*
	'}'
;
	
Annotation returns modulob::Annotation:
	'@' option=ConfigOption (':' value=ConfigValue)?
;	
	
enum ConfigOption returns modulob::ConfigOption:
	persistency = 'persistency'
;	
	
enum ConfigValue returns modulob::ConfigValue:
	CoreData = 'CoreData' 
;	
	
Feature returns modulob::Feature:
	(comment = DOC_COMMENT)?
	(isIndexed?='indexed')?
	(isContainment?='-[')?
	type=TypeRef name=ID ('=' defaultValue=ValueObject)? 
	('<->' inverse=[modulob::Feature | QualifiedName])?
	';'
;

Enum returns modulob::Enum:
	'enum' name = ID '{'
		literals += EnumLiteral (',' literals += EnumLiteral)*
	'}'
;

EnumLiteral returns modulob::EnumLiteral:
	name = ID
;

